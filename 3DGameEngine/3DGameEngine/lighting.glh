uniform vec3 C_eyePos;
uniform float specularIntensity;
uniform float specularPower;

struct BaseLight
{
	vec3 color;
	float intensity;
};

struct Attenuation
{
	float constant;
	float linear;
	float exponent;
};

struct DirectionalLight
{
	BaseLight base;
	vec3 direction;
};

struct PointLight
{
	BaseLight base;
	Attenuation atten;
	vec3 position;
	float range;
};

struct SpotLight
{
	PointLight pLight;
	vec3 direction;
	float cutoff;
};

vec4 calcLight(BaseLight base, vec3 direction, vec3 normal, vec3 worldPos)
{
	float diffuseFactor = dot(normal, -direction);

	vec4 diffuseColor = vec4(0,0,0,0);
	vec4 specularColor = vec4(0,0,0,0);

	if(diffuseFactor > 0)
	{
		diffuseColor = vec4(base.color, 1.0) * base.intensity * diffuseFactor;

		vec3 eyeToPointDirection = normalize(C_eyePos - worldPos);
		vec3 reflectDirection = normalize(reflect(direction, normal));
		
		float specularFactor = dot(eyeToPointDirection, reflectDirection);
		specularFactor = pow(specularFactor, specularPower);

		if(specularFactor > 0)
		{
			specularColor = vec4(base.color, 1.0) * specularIntensity * specularFactor;
		}
	}

	return diffuseColor + specularColor;
}

vec4 calcDirectionalLight(DirectionalLight dLight, vec3 normal, vec3 worldPos)
{
	return calcLight(dLight.base, -dLight.direction, normal, worldPos);
}

vec4 calcPointLight(PointLight pLight, vec3 normal, vec3 worldPos)
{
	vec3 lightDirection =  pLight.position - worldPos;
	float distanceToPoint = length(lightDirection);
	
	if(distanceToPoint > pLight.range) //performance optimization
	{
		return vec4(0,0,0,0);
	}

	lightDirection = normalize(lightDirection);

	vec4 color = calcLight(pLight.base, -lightDirection, normal, worldPos);

	float attenuation = pLight.atten.constant + pLight.atten.linear * distanceToPoint + pLight.atten.exponent * distanceToPoint *distanceToPoint + 0.00001;

	return color / attenuation;
}

vec4 calcSpotLight(SpotLight sLight, vec3 normal, vec3 worldPos)
{
	vec3 lightDirection = normalize(worldPos - sLight.pLight.position);
	float spotFactor = dot(lightDirection, sLight.direction);
	
	vec4 color = vec4(0,0,0,0);

	if(spotFactor > sLight.cutoff)
	{
		color = calcPointLight(sLight.pLight, normal, worldPos) * (1.0 - (1.0 - spotFactor)/(1.0 - sLight.cutoff));
	}

	return color;
}
